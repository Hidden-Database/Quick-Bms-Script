# Unreal Engine 4 - Unreal Tournament 4 (*WindowsNoEditor.pak) (script 0.4.23)
# script for QuickBMS http://quickbms.aluigi.org

# It only works with 1.0.x versions of the game at the moment!

math NO_TAIL_INFO = 0   # set it to 1 for archives with corrupt/missing tail information

quickbmsver "0.8.4"

math TOC_FILE = 0

# set your AES_KEY here as text or C string ("\x11\x22...") or umodel hex (0x1122...)
set AES_KEY binary ""
putarray 10 -1 ""
math AES_KEY_IS_SET = 0

math ALTERNATIVE_MODE = 0

get ARCHIVE_NAME basename
get ARCHIVE_PATH FILEPATH

get HASH_NAME basename
string HASH_NAME + ".hashes"
set TMP1 binary "\x0A"
set TMP2 binary "\x20"
log MEMORY_FILE5 0 0
math TOC_FILE2 = -5

math VERSION = 0

math CHUNK_OFFSET_ABSOLUTE = -1 # default, enabled

# 1 = HIT
math WORKAROUND = 0

if NO_TAIL_INFO != 0
    get OFFSET asize
    math ALTERNATIVE_MODE = 1
else
    goto -0xac # for version 8 (4.22)
	savepos MAGIC_OFF
	get MAGIC long  #idstring "\xe1\x12\x6f\x5a" # 0x5a6f12e1
	
	if MAGIC != 0x5a6f12e1
		goto -0x2c # previous versions 3-7
		savepos MAGIC_OFF
		get MAGIC long
	else
		math WORKAROUND = 4
	endif
    
    if MAGIC == 0
        goto -0xcc  # version 8 (4.23-4.24)
        savepos MAGIC_OFF
        get MAGIC long
        math WORKAROUND = 3
    endif

    endian guess MAGIC      # 0x5a6f12e1:le, 0xe1126f5a:be
    if MAGIC == 0x1233a     # HIT game
        math WORKAROUND = 1
    elif MAGIC == 0x1066a   # OverHit
        math WORKAROUND = 1
    elif MAGIC == 0x13aed   # OverHit
        math WORKAROUND = 1
    elif ARCHIVE_NAME & "OverHit"
        math WORKAROUND = 1
    elif ARCHIVE_NAME == "0"    # HIT
        math WORKAROUND = 1
    elif ARCHIVE_PATH & "Gears of War 4"
        math WORKAROUND = 2
    endif
    get VERSION long
    endian guess VERSION
	
	goto SEEK_SET
	print "Work in progress..."
	findloc OFFSET binary "\x79\x79\x79\x57\x57\x56\x57\x57" 0 ""
	if OFFSET == ""
		print "This pak format is not supported!"
		cleanexit
	endif
	math OFFSET - 1
	#set SIZE longlong 0
	#xmath SIZE "OFFSET - MAGIC_OFF - 17"

    if WORKAROUND == 1
        math VERSION = 3
    endif
	
    if VERSION >= 3
        goto MAGIC_OFF
        goto -1 0 SEEK_CUR
        get ENCRYPTED byte
        if ENCRYPTED != 0
            #callfunction SET_AES_KEY 1
            #log MEMORY_FILE10 OFFSET SIZE
            #encryption "" ""
            #math TOC_FILE = -10 # MEMORY_FILE10 is -10
        endif
    endif

    goto OFFSET # don't use TOC_FILE
    callfunction GET_NAME 1
endif

if ALTERNATIVE_MODE == 0
    get FILES long TOC_FILE
else
    math FILES = 0x7fffffff
    math MAX_OFF = OFFSET
    goto 0
    set NAME string ""
endif

math CHUNK_SIZE = 0x10000   # just in case...
for i = 0 < FILES
    if ALTERNATIVE_MODE == 0
        callfunction GET_NAME 1
        if NAMESZ == 0  # lame work-around for Conan Exiles
            callfunction GET_NAME 1
        endif
    endif
    savepos TMP_OFF TOC_FILE
	
	getdstring HASH 20
	get OFFSET longlong TOC_FILE
	get SIZE longlong TOC_FILE
	get ZIP long TOC_FILE
    get ZSIZE longlong TOC_FILE
	getdstring DUMMY2 21 TOC_FILE

	string HASH 0b HASH
	putdstring HASH 40 TOC_FILE2
	putct TMP2 string -1 TOC_FILE2
	putct NAME string -1 TOC_FILE2
	putct TMP1 string -1 TOC_FILE2
	
    math CHUNKS = 0
    math ENCRYPTED = 0
    if VERSION >= 3
        if ZIP != 0
            get CHUNKS long TOC_FILE
            for x = 0 < CHUNKS
                get CHUNK_OFFSET longlong TOC_FILE
                get CHUNK_END_OFFSET longlong TOC_FILE
                putarray 0 x CHUNK_OFFSET
                putarray 1 x CHUNK_END_OFFSET
            next x
        endif
		get CHUNK_SIZE long TOC_FILE
        if WORKAROUND == 2
        else
            get ENCRYPTED byte TOC_FILE
        endif
        
    endif
    if WORKAROUND == 1
        if ARCHIVE_NAME == "0"  # HIT game only!
            math ENCRYPTED = 0
        endif
    endif
    if ALTERNATIVE_MODE != 0
        savepos TMP_OFF TOC_FILE
        math OFFSET + TMP_OFF
    endif

    if ALTERNATIVE_MODE != 0
        math OFFSET + ZSIZE
        goto OFFSET
        if OFFSET == MAX_OFF
            break
        endif
        if VERSION >= 8
            padding 0x800   # necessary for WitchIt
        endif
    endif
next i

get SIZE asize TOC_FILE2
log HASH_NAME 0 SIZE TOC_FILE2
print "Done!"

startfunction SET_AES_KEY_ASK
    math AES_KEY_IS_SET = 1
    print "The archive is encrypted, select the number of the key to use or type yours:"
    for z = 0
        getarray KEY 10 z
        if KEY == ""
            break
        endif
        print "%z%: %KEY%"
    next z
        print "%z%: press RETURN for no encryption (Lineage 2 Revolution)"
    set KEY unknown "???"
    strlen TMP KEY
    if KEY == ""
        math AES_KEY_IS_SET = -1
        set AES_KEY string "no key, encryption disabled"
    elif TMP <= 2
        getarray AES_KEY 10 KEY
        if AES_KEY strncmp "0x"
            string AES_KEY << 2
            string AES_KEY h AES_KEY
        endif
    elif KEY strncmp "0x"
        string KEY << 2
        string AES_KEY h KEY
    else
        set AES_KEY binary KEY
    endif
    print "KEY: %AES_KEY%"
endfunction

startfunction SET_AES_KEY
    if AES_KEY_IS_SET == 0
        callfunction SET_AES_KEY_ASK 1
    endif
    if AES_KEY_IS_SET > 0
        encryption aes AES_KEY "" 0 32
    endif
endfunction

startfunction GET_NAME
	filexor 0x79 
    get NAMESZ signed_long TOC_FILE
    if NAMESZ >= 0
        if NAMESZ > 0x200
            math ALTERNATIVE_MODE = 1
        else	
            getdstring NAME NAMESZ TOC_FILE
        endif
    else
        math NAMESZ n NAMESZ
        if NAMESZ > 0x200
            math ALTERNATIVE_MODE = 1
        else
            math NAMESZ * 2
            getdstring NAME NAMESZ TOC_FILE
            set NAME unicode NAME
        endif
    endif
endfunction
